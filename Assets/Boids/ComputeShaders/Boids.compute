
// 指定内核函数
#pragma kernel ForceCS      // 操舵力
#pragma kernel IntegrateCS  // 速度, 位置 计算

// Boid数据结构
struct BoidData
{
	float3 velocity; // 速度
	float3 position; // 位置
};

// 线索组线索大小
#define SIMULATION_BLOCK_SIZE 256

// Boid 数据缓冲器(用于读取)
StructuredBuffer<BoidData>   _BoidDataBufferRead;
// Boid 数据缓冲器(读取,写入)
RWStructuredBuffer<BoidData> _BoidDataBufferWrite;
// Boid 操舵力的缓冲器（读取用）
StructuredBuffer<float3>     _BoidForceBufferRead;
// Boid 操舵力的缓冲器（读取,写入）
RWStructuredBuffer<float3>   _BoidForceBufferWrite;

int _MaxBoidObjectNum; // Boid 对象数量

float _DeltaTime;      // 前一帧经过的时间

float _SeparateNeighborhoodRadius;  // 与其他个体应用分离的距离
float _AlignmentNeighborhoodRadius; // 与要对齐的另一个体的距离
float _CohesionNeighborhoodRadius;  // 应用耦合的其它个体的距离

float _MaxSpeed;        // 速度の最大値
float _MaxSteerForce;   // 操舵力量的最大值

float _SeparateWeight;  // 分離適用時の重み
float _AlignmentWeight; // 整列適用時の重み
float _CohesionWeight;  // 結合適用時の重み

float4 _WallCenter;      // 壁の中心座標
float4 _WallSize;        // 壁のサイズ
float  _AvoidWallWeight; // 避开墙壁的强度的重量


// 限制矢量的大小
float3 limit(float3 vec, float max)
{
	float length = sqrt(dot(vec, vec)); // 大きさ
	return (length > max && length > 0) ? vec.xyz * (max / length) : vec.xyz;
}

// 碰到墙壁时还逆向的力量
float3 avoidWall(float3 position)
{
	float3 wc = _WallCenter.xyz;
	float3 ws = _WallSize.xyz;
	float3 acc = float3(0, 0, 0);
	// x
	acc.x = (position.x < wc.x - ws.x * 0.5) ? acc.x + 1.0 : acc.x;
	acc.x = (position.x > wc.x + ws.x * 0.5) ? acc.x - 1.0 : acc.x;
	
	// y
	acc.y = (position.y < wc.y - ws.y * 0.5) ? acc.y + 1.0 : acc.y;
	acc.y = (position.y > wc.y + ws.y * 0.5) ? acc.y - 1.0 : acc.y;
	
	// z
	acc.z = (position.z < wc.z - ws.z * 0.5) ? acc.z + 1.0 : acc.z;
	acc.z = (position.z > wc.z + ws.z * 0.5) ? acc.z - 1.0 : acc.z;

	return acc;
}

// 共享存储器Boid数据存储用
groupshared BoidData boid_data[SIMULATION_BLOCK_SIZE];

// 操舵力计算用内核函数
[numthreads(SIMULATION_BLOCK_SIZE, 1, 1)]
void ForceCS(
	uint3 DTid : SV_DispatchThreadID, // 全部线程固有的ID
	uint3 Gid : SV_GroupID,           // 组合ID
	uint3 GTid : SV_GroupThreadID,    // 组内的线索ID
	uint  GI : SV_GroupIndex          // SV_GroupThreadID 设为一元的 0-255
)
{
	const unsigned int P_ID = DTid.x; // 自身のID
	float3 P_position = _BoidDataBufferRead[P_ID].position; // 自身の位置
	float3 P_velocity = _BoidDataBufferRead[P_ID].velocity; // 自身の速度

	float3 force = float3(0, 0, 0); // 操舵力を初期化

	float3 sepPosSum = float3(0, 0, 0); // 分離計算用 位置加算変数
	int sepCount = 0; // 分離のために計算した他の個体の数のカウント用変数

	float3 aliVelSum = float3(0, 0, 0); // 整列計算用 速度加算変数
	int aliCount = 0; // 整列のために計算した他の個体の数のカウント用変数

	float3 cohPosSum = float3(0, 0, 0); // 結合計算用 位置加算変数
	int cohCount = 0; // 結合のために計算した他の個体の数のカウント用変数

	// SIMULATION_BLOCK_SIZE（グループスレッド数）ごとの実行 (グループ数分実行)
	[loop]
	for (uint N_block_ID = 0; N_block_ID < (uint)_MaxBoidObjectNum;
		N_block_ID += SIMULATION_BLOCK_SIZE)
	{
		// SIMULATION_BLOCK_SIZE分のBoidデータを、シェアードメモリに格納
		boid_data[GI] = _BoidDataBufferRead[N_block_ID + GI];

        //完成所有组共享访问，
        //直到组内的所有线程到达此呼叫，
        //阻止执行组内所有线索
		GroupMemoryBarrierWithGroupSync();

		// 他の個体との計算
		for (int N_tile_ID = 0; N_tile_ID < SIMULATION_BLOCK_SIZE; N_tile_ID++)
		{
			float3 N_position = boid_data[N_tile_ID].position; // 他の個体の位置
			float3 N_velocity = boid_data[N_tile_ID].velocity; // 他の個体の速度

			float3 diff = P_position - N_position; // 自身と他の個体の位置の差
			float  dist = sqrt(dot(diff, diff));   // 自身と他の個体の位置の距離

			// --- 分離（Separation） ---
			if (dist > 0.0 && dist <= _SeparateNeighborhoodRadius)
			{
				// 他の個体の位置から自身へ向かうベクトル
				float3 repulse = normalize(P_position - N_position);
				// 自身と他の個体の位置の距離で割る（距離が遠ければ影響を小さく）
				repulse /= dist;
				sepPosSum += repulse; // 加算
				sepCount++;           // 個体数カウント
			}

			// --- 整列（Alignment） ---
			if (dist > 0.0 && dist <= _AlignmentNeighborhoodRadius)
			{
				aliVelSum += N_velocity; // 加算
				aliCount++;              // 個体数カウント
			}

			// --- 結合（Cohesion） ---
			if (dist > 0.0 && dist <= _CohesionNeighborhoodRadius)
			{
				cohPosSum += N_position; // 加算
				cohCount++;              // 個体数カウント 
			}
		}
		GroupMemoryBarrierWithGroupSync();
	}

	// 操舵力（分離）
	float3 sepSteer = (float3)0.0;
	if (sepCount > 0)
	{
		sepSteer = sepPosSum / (float)sepCount;     // 平均を求める
		sepSteer = normalize(sepSteer) * _MaxSpeed; // 最大速度に調整
		sepSteer = sepSteer - P_velocity;           // 操舵力を計算
		sepSteer = limit(sepSteer, _MaxSteerForce); // 操舵力を制限
	}

	// 操舵力（整列）
	float3 aliSteer = (float3)0.0;
	if (aliCount > 0)
	{
		aliSteer = aliVelSum / (float)aliCount;     // 近い個体の速度の平均を求める
		aliSteer = normalize(aliSteer) * _MaxSpeed; // 最大速度に調整
		aliSteer = aliSteer - P_velocity;           // 操舵力を計算
		aliSteer = limit(aliSteer, _MaxSteerForce); // 操舵力を制限
	}
	// 操舵力（結合）
	float3 cohSteer = (float3)0.0;
	if (cohCount > 0)
	{
		cohPosSum = cohPosSum / (float)cohCount;    // 近い個体の位置の平均を求める
		cohSteer = cohPosSum - P_position;      // 平均位置方向へのベクトルを求める
		cohSteer = normalize(cohSteer) * _MaxSpeed; // 最大速度に調整
		cohSteer = cohSteer - P_velocity;           // 操舵力を計算
		cohSteer = limit(cohSteer, _MaxSteerForce); // 操舵力を制限
	}
	force += aliSteer * _AlignmentWeight; // 操舵力に整列する力を加える
	force += cohSteer * _CohesionWeight;  // 操舵力に結合する力を加える
	force += sepSteer * _SeparateWeight;  // 操舵力に分離する力を加える

	_BoidForceBufferWrite[P_ID] = force; // 書き込み
}

// 速度, 位置計算用カーネル関数
[numthreads(SIMULATION_BLOCK_SIZE, 1, 1)]
void IntegrateCS
(
	uint3 DTid : SV_DispatchThreadID // スレッド全体で固有のID
)
{
	const unsigned int P_ID = DTid.x;          // インデックスを取得
											           
	BoidData b = _BoidDataBufferWrite[P_ID];   // 現在のBoidデータを読み込む
	float3 force = _BoidForceBufferRead[P_ID]; // 操舵力を読み込む
	
	// 壁に近づいたら反発する力を与える
	force += avoidWall(b.position) * _AvoidWallWeight; 

	b.velocity += force * _DeltaTime;          // 操舵力を速度に適用
	b.velocity = limit(b.velocity, _MaxSpeed); // 速度を制限
	b.position += b.velocity * _DeltaTime;     // 位置を更新
											           
	_BoidDataBufferWrite[P_ID] = b;            // 計算結果を書き込む
}